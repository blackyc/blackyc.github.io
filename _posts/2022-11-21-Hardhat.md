---
layout: post
title:  "hardhat&foundry"
date:   2022-11-25 17:16:00 +0800
tags: hardhat
color: rgb(98,222,189)
cover: '../assets/papercover/hmbb.png'
subtitle: 'hardhat&foundry by yako33!'
---

### hardhat&foundry自动化合约部署与测试，模拟攻击

[TOC]

thanks by Frank & Deft(beosin security team)

#### 基础环境
当前实验环境  
&emsp;&emsp;windows 10  
&emsp;&emsp;Node.js 16.14.0  
以空文件夹中加入某ERC20示例代币为例，目录结构(contracts不要命名错误)  
&emsp;&emsp;testhardhat  
&emsp;&emsp;&emsp;&emsp;contracts  
&emsp;&emsp;&emsp;&emsp;&emsp;&emsp;ERC20.sol

#### 创建hardhat项目
位于testhardhat文件下使用Powershell命令初始化
```js
npm init -y
```
此时会生成package.json，接下来安装Hardhat，这个过程可能会需要耗费些许时间
```js
npm install hardhat
```
安装结束之后创建项目，官方推荐npx的使用在项目中进行，因为可以根据项目版本进行合理控制。
```js
npx hardhat
```
选择Create an empty hardhat.config.js运行后则会在项目目录中创建一个Hardhat配置文件（hardhat.config.js）
<ul>
<li  markdown="1" style="list-style-type: none;">
![1]({{site.url}}/screenshot/20221121hardhat/1.png)
</li>
</ul>



Hardhat项目创建完毕后，可以安装Ethers plugin，为了简化交互
```
npm install @nomiclabs/hardhat-ethers ethers
npm install @nomiclabs/hardhat-waffle
```

为了配置与布局方便，手动添加一个secrets.json文件，用于保存私钥,以ganacha为例，这里配置的是ganache的账户私钥 
```js
{
    "privateKey0":"c02541627e8811d4038e2aa96dc613fa6ae2372b993da27ddeb3955b948aad21",
    "privateKey1":"3b9dcc325007c1c616b4b06033a9013a599fdaaa7e770e0f656f7d0710436615",
    "privateKey2":"d45e2033d7214c170311872625c8b47d87d699e538cd7c7da9abf83d7b76151b"
}
```

接下来就可以修改hardhat.config.js文件,此文件中默认为下面代码，而实际上需要根据情况修改solidity的版本,当前测试代币合约为大于0.5.0
```js
/** @type import('hardhat/config').HardhatUserConfig */
module.exports = {
  solidity: "0.8.17",
};
```

通过require导入插件与私钥配置文件
```js
// Import the Ethers plugin required to interact with the contract
require('@nomiclabs/hardhat-ethers');

// Key config path
const { privateKey0 } = require('./secrets.json');

/** @type import('hardhat/config').HardhatUserConfig */
module.exports = {
  solidity: "0.5.17",
};
```

接下来是添加配置代码，使用的网络。
```js
networks:{
    // testnet
    testnet: { //任意的链网络的rpc地址，
      url: `https://rpc.testnet.fantom.network/`,
      chainId: 4002, //chainID
      accounts: [privateKey0], //私钥配置文件中的私钥0
      gas:8000000
    },

    local_network:{
    url: `HTTP://127.0.0.1:7545`,
    accounts: [privateKey0],
    gas:6721975000
    }
}
```

最终代码为
```js
// Import the Ethers plugin required to interact with the contract
require('@nomiclabs/hardhat-ethers');

// Key config path
const { privateKey0 } = require('./secrets.json');

/** @type import('hardhat/config').HardhatUserConfig */
module.exports = {
  solidity: {compilers:[{version:"0.5.17",settings:{
    optimizer: {
      enabled: true,
      runs: 1000
    }
  }}]},

  networks: {
    // testnet
    testnet: { //任意的链网络的rpc地址，
      url: `https://rpc.testnet.fantom.network/`,
      chainId: 4002, //chainID
      accounts: [privateKey0], //私钥配置文件中的私钥0
      gas:8000000
    },
  
    local_network:{
    url: `HTTP://127.0.0.1:8545`, //这里配置为默认ganacha地址
    accounts: [privateKey0],
    gas:6721975000 //每笔交易的上限，即默认的gaslimt
    }
  }
};
```

#### 部署代码
上面的代码写好之后，可以先进行编译检查是否有错误，包是否配置完毕。successfully则表示编译完成，会生成artifacts与cache文件夹
```js
npx hardhat compile
```


接下来为方便布局使用创建./scripts/deploy.js，以如下代码布局进行合约部署
```js
const { ethers  } = require("hardhat");
const { privateKey0, privateKey1 , privateKey2 } = require('../secrets.json');

// 全局定义钱包实例
const url = 'HTTP://127.0.0.1:8545';
const Provider = new ethers.providers.JsonRpcProvider(url);
const owner = new ethers.Wallet(privateKey0, Provider)
const wallet1 = new ethers.Wallet(privateKey1, Provider);
const wallet2 = new ethers.Wallet(privateKey2, Provider);

//部署函数
async function Deploy() {
    console.log(
        "Deploy Complete !"
    );
}

//模拟主入口
async function main(){
    Deploy();
}

// try catch for err
main().catch((error) => {
    console.error(error);
    process.exitCode = 1;
});
```

目前我们除了在console中打印日志没有对合约进行任何操作，使用下面的命令，指定配置的本地网络进行测试
```
npx hardhat run --network local_network scripts/deploy.js
```
如果没有报错则可以看到控制台打印"Deploy Complete !" (如果报const error/Error: could not detect network检查config中网络是否配置错误)

#### 合约交互

部署合约并打印合约地址
```js
//部署函数
async function Deploy() {
    //参数为要部署的合约名称，ERC20中代币主合约命OLPC所以这里使用这个名字。
    const OLPC = await ethers.getContractFactory("OLPC");
    const olpc = await OLPC.deploy();
    console.log('Deploying OPLC...');
    //等等合约部署完成
    await olpc.deployed();
    console.log('OLPC address in:  ',olpc.address)

    console.log(
        "Deploy Complete !"
    );
}
```

部署中如果有参数需要填写也可以直接加入参数，比如
```js
const PancakeRouter = await pancakeRouter.deploy(MdexFactory.address, WETH.address);
```
也可以调用合约函数，比如获得uniswap部署时的initcodehash
```js
//部分代码
const mdexFactory = await ethers.getContractFactory("MdexFactory");
...
const MdexFactory = await mdexFactory.deploy(wallet2.address);
...
const initCodeHash = await MdexFactory.getInitCodeHash();
...
await MdexFactory.connect(wallet2).setInitCodeHash(initCodeHash);
```

如果对于已经部署或者需要初始化的函数则可以直接调用，例如直接调用launchPool初始化
```js
await poolFactory.deployed();
await poolFactory.launchPool(
  10,1669015872,1669019472,10,1000000,
  owner.address,bUSD.address
);
```

当遇见部署或者调用测试脚本时可能遇见不明确的调试信息，这个时候可以尝试直接使用run或者直接调用脚本不指定网络，可能会给出比较正确的报错信息,例如报错数据类型未指定但 又正确声明后的情况。
```js
npx hardhat run // npx hardhat run scripts/deploy.js
```


#### 使用hardhat编写测试脚本
为布局创建并使用 ./test/test.js，实际生产中可能也会由ts编写
需要npm安装chai，这是一个断言库文件，安全完毕后可以简单写一个测试文件对刚刚部署的代币进行测试。
```js
//这是一个断言库
const { expect } = require("chai");

//名称
describe("OLPC", function() {
    //初始化2个账户地址
    async function deploy() {
        const [owner, otherAccount] = await ethers.getSigners();
        const OLPC = await ethers.getContractFactory("OLPC");
        const oplc = await OLPC.deploy();
        await oplc.deployed();
        console.log("owner:  ",owner.address);
        //返回合约，owner方便其他函数调用
        return{ oplc, owner, otherAccount}
    }

    it("Should set the right owner", async function () {
        const { oplc, owner, otherAccount } = await deploy();
        //调用合约中的已有函数
        const balance = oplc.balanceOf(owner.address);
        console.log("owner balance: ", balance); 
        //判断预铸到owner账户是否成功
        expect(await balance).to.equal(5130000 * (10 ** 6));
        console.log("right!");
    });
});
```
确保使用的命令为
```js
npx hardhat test
```
<ul>
<li  markdown="1" style="list-style-type: none;">
![2]({{site.url}}/screenshot/20221121hardhat/2.png)
</li>
</ul>

#### 在合约中使用console.log获取信息
首先需要在合约中导入文件
```js
pragma solidity ^0.5.0;

import "hardhat/console.sol";
```
接下来在需要使用的函数中加入console.log

```js
function balanceOf(address account) public view returns (uint256) {
    console.log("Sender balance is %s tokens", _balances[account]);
    return _balances[account];
}
```
<ul>
<li  markdown="1" style="list-style-type: none;">
![3]({{site.url}}/screenshot/20221121hardhat/3.png)
</li>
</ul>

#### hardhat fork链状态

启动一个Fork主网的Hardhat Network实例，本地测试复杂的交互,需要启动一个节点来完成，官方推荐Alchemy  
[https://learnblockchain.cn/docs/hardhat/guides/mainnet-forking.html](https://learnblockchain.cn/docs/hardhat/guides/mainnet-forking.html)

拉取节点进行测试
```js
npx hardhat node --fork https://eth-mainnet.alchemyapi.io/v2/<key>
```

也可以使用--fork-block-number指定区块
```js
npx hardhat node --fork https://eth-goerli.g.alchemy.com/v2/w-pIbpU...tZuzRC9 --fork-block-number 7974072
```

或者使用配置文件fork某个区块上的网络状态，如果不写区块会默认拉取最新的
```js
    //fork goerli test 固定写法，内置定义，hardhat testnet loacl_network，不能自定义。
    hardhat: {
      forking: {
        url: "https://eth-goerli.g.alchemy.com/v2/w-pIbpUV...tZuzRC9",
        blockNumber: 7974072
      }
    }
```

直接使用命令会默认使用配置的网络，默认会使用8545，需要关闭Ganacha
```js
npx hardhat node
```
<ul>
<li  markdown="1" style="list-style-type: none;">
![4]({{site.url}}/screenshot/20221121hardhat/4.png)
</li>
</ul>

此时在调用测试脚本时则会使用fork网络

<ul>
<li  markdown="1" style="list-style-type: none;">
![5]({{site.url}}/screenshot/20221121hardhat/5.png)
</li>
</ul>


在test文件夹下不同的js测试脚本可以指定不同的fork状态，或者重置，params传递空参数则可以禁用
```js
await network.provider.request({
  method: "hardhat_reset",
  params: [{
    forking: {
      jsonRpcUrl: "https://eth-mainnet.alchemyapi.io/v2/<key>",
      blockNumber: 11095000
    }
  }]
})
```

#### yarn 相关使用

(待更新)

#### hardhat 模拟攻击

(待更新)
目前来看因为学习成本等原因，使用hardhat来完成不太合适，一方面是语言，一方面写起来非常麻烦，暂时研究foundry


#### foundry 基本环境

使用foundry需要foundry环境，并且需要rust支持,跟进此处[instructions](https://book.getfoundry.sh/getting-started/installation)按照必要的环境  

在控制台安装好之后，在项目中运行，这可能会发花费较长的时间
```
cargo install --git https://github.com/foundry-rs/foundry foundry-cli anvil --bins --locked
```

(待更新)


#### 用代币来熟悉foundry基本操作

[方法来源引用链接](https://cloud.tencent.com/developer/article/2010589)

创建空模板，这一步可能会因为网络或者各种原因各种报错，只能多次安装或者更新。
```
forge init
```
下载OpenZeppelin包并且更新std库
```
forge install OpenZeppelin/openzeppelin-contracts@v4.5.0
forge update foundry-rs/forge-std
```

由于安装多次失败，使用参考文章中的模板进行初始化。
```
forge init --template https://github.com/soliditylabs/forge-erc20-template
```

关于配置文件
```
# Additional configuration and default values at https://github.com/gakonst/foundry/tree/master/config
[default]

# Sets the concrete solc version to use
# This overrides the `auto_detect_solc` / value auto_detect_solc = true
# 这里是注释了的，可以指定solidity的版本
# solc_version = '0.8.13'

# 指定配置项目的路径，这里是lib/forge-std和openzeppelin
remappings = [
  "ds/=lib/ds-test/src/",
  "@std=lib/forge-std/src/",
  "@openzeppelin=lib/openzeppelin-contracts/contracts/",
]

[ci]
fuzz_runs = 2_000

# 资料夹
# src = 'src'
# 測試檔資料夾
# test = 'test'
# Artifact 資料夾
# out = 'out'

# RPC url。注意如果有提供這個 url，測試會默認你是要用 fork network 執行測試 #eth_rpc_url = 'https://eth-mainnet.alchemyapi.io/v2/API_KEY'
```



基本代币
```js
// SPDX-License-Identifier: MIT
pragma solidity 0.8.13;

import {ERC20} from "openzeppelin-contracts/token/ERC20/ERC20.sol";

contract MyERC20 is ERC20 {
    constructor() ERC20("Name", "SYM") {
        this;
    }
}
```

测试合约,根据我写的注释来看具体作用
```js
// SPDX-License-Identifier: MIT
pragma solidity 0.8.13;

//导入std的包来输出信息，导入测试用的包, /forge-std/src/test/console.sol
import {console} from "forge-std/console.sol";
import {stdStorage, StdStorage, Test} from "forge-std/Test.sol";

//这2个文件继承了DStest里面有许多使用的函数，创建，断言等等
import {Utils} from "./utils/Utils.sol";
import {MyERC20} from "../MyERC20.sol";

contract BaseSetup is MyERC20, Test {
    Utils internal utils;
    address payable[] internal users;

    address internal alice;
    address internal bob;
    //Forge中的setUp关键字在每个测试用例运行之前调用的可选函数
    //还有其他关键词作为函数命名的前缀，test作为测试用例，testFail等等
    function setUp() public virtual {
        utils = new Utils(); //初始化Utils
        users = utils.createUsers(2); //调用创建方法可以创建2个账户

        alice = users[0]; //在vm中加标签，在堆栈跟踪容易识别
        vm.label(alice, "Alice"); //关于VM众多功能的查看 forge-std/src/Vm.sol
        bob = users[1];
        vm.label(bob, "Bob");
    }
}

contract WhenTransferringTokens is BaseSetup {
    uint256 internal maxTransferAmount = 12e18;
    //类似于beforeEach和describe
    function setUp() public virtual override {
        BaseSetup.setUp(); //调用BaseSetup初始化账户
        console.log("When transferring tokens");//使用console.log来打印日志信息
    }

    function transferToken(
        address from,
        address to,
        uint256 transferAmount
    ) public returns (bool) {
        vm.prank(from); //prank(address),将所有后续调用的 msg.sender 设置为输入地址
        //因为prank修改了调用者，这里使用的this
        return this.transfer(to, transferAmount);
    }
}

contract WhenAliceHasSufficientFunds is WhenTransferringTokens {
    using stdStorage for StdStorage;
    uint256 internal mintAmount = maxTransferAmount;
    //before初始化
    function setUp() public override {
        WhenTransferringTokens.setUp();
        console.log("When Alice has sufficient funds");
        _mint(alice, mintAmount);
    }

    function itTransfersAmountCorrectly(
        address from,
        address to,
        uint256 transferAmount
    ) public {
        uint256 fromBalanceBefore = balanceOf(from);
        bool success = transferToken(from, to, transferAmount);
        // src/test/utils，forge-std中也有，断言函数有相等小于大于(eq,le,ge)  assertTrue判断返回值
        assertTrue(success);
        assertEqDecimal(
            balanceOf(from),
            fromBalanceBefore - transferAmount,
            decimals()
        );
        assertEqDecimal(balanceOf(to), transferAmount, decimals());
    }
    //带参调用
    function testTransferAllTokens() public {
        itTransfersAmountCorrectly(alice, bob, maxTransferAmount);
    }

    function testTransferHalfTokens() public {
        itTransfersAmountCorrectly(alice, bob, maxTransferAmount / 2);
    }

    function testTransferOneToken() public {
        itTransfersAmountCorrectly(alice, bob, 1);
    }

    function testTransferWithFuzzing(uint64 transferAmount) public {
        vm.assume(transferAmount != 0);
        itTransfersAmountCorrectly(
            alice,
            bob,
            transferAmount % maxTransferAmount
        );
    }
    //签名打包的方式判断，感觉实际上用的很少，这里使用了mockcall模拟调用，如果调用成功会返回ture，使用完之后清空模拟
    function testTransferWithMockedCall() public {
        vm.prank(alice);
        vm.mockCall(
            address(this),
            abi.encodeWithSelector(
                this.transfer.selector,
                bob,
                maxTransferAmount
            ),
            abi.encode(false)
        );
        bool success = this.transfer(bob, maxTransferAmount);
        assertTrue(!success);
        vm.clearMockedCalls();
    }

    //vm.load(地址，byte32数据)，从状态读取数据。
    function testFindMapping() public {
        uint256 slot = stdstore
            .target(address(this))
            .sig(this.balanceOf.selector)
            .with_key(alice)
            .find();
        bytes32 data = vm.load(address(this), bytes32(slot));
        assertEqDecimal(uint256(data), mintAmount, decimals());
    }
}
```

做一个带有输入变量的测试函数，forge 会自动进行模糊测试vm.assume排除值，modulo限制为某个值
```
function testTransferFuzzing(uint64 amount) public {
    vm.assume(amount != 0);
    itTransfersAmountCorrectly(
        alice,
        bob,
        amount % maxTransferAmount
    );
}
```

依据V数量不同打印不同级别的告警信息。
```
forge test -vvvvv
```
2: 打印所有测试的日志
3: 打印失败的测试的执行堆栈
4: 打印所有测试的执行堆栈，以及失败测试的 setup 堆栈
5: 打印所有测试的执行和 setup 堆栈


<ul>
<li  markdown="1" style="list-style-type: none;">
![1]({{site.url}}/screenshot/20221121hardhat/6.png)
</li>
</ul>


其余功能非常多结合漏洞分析一起使用。

#### foundry 模拟攻击

需要配置RPC
```
rpc_endpoints = { 
    mainnet = "https://rpc.ankr.com/eth", 
    optimism = "https://rpc.ankr.com/optimism" , 
    fantom = "https://rpc.ankr.com/fantom", 
    arbitrum = "https://rpc.ankr.com/arbitrum", 
    bsc = "https://rpc.ankr.com/bsc", 
    moonriver = "https://moonriver.public.blastapi.io", 
    gnosis = "https://rpc.ankr.com/gnosis", 
    avax = "https://rpc.ankr.com/avalanche", 
    polygon = "https://rpc.ankr.com/polygon", 
    eth_goerli="https://rpc.ankr.com/eth_goerli"
}
```

链fork区块状态
```
function setUp() public {
    cheats.createSelectFork("bsc", 22832427);
}
```

示例代码
```
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.10;

import "forge-std/Test.sol";
import "./interface.sol";

interface Ft {
    function Flag() external returns(bool);
}

contract testFoundry is DSTest {
    //0x71097是固定的修改地址，不能更改,这个地址主要是为了进行作弊选项
    CheatCodes cheats = CheatCodes(0x7109709ECfa91a80626fF3989D68f67F5b1DD12D);
    address constant Flag = 0x7067222c38268E25E53Fb00aBe6D652EBd38d213;

    function setUp() public{
        cheats.createSelectFork("eth_goerli",8053179);
        console.log("is fork?");
    }

    function testExploit() public{
        console.log("***start***");
        //测试网中部署的测试合约
        bool suc = Ft(0x7067222c38268E25E53Fb00aBe6D652EBd38d213).Flag();
        require(suc, "fLAG = false");
        console.log("***end***");
    }

}
```

<ul>
<li  markdown="1" style="list-style-type: none;">
![1]({{site.url}}/screenshot/20221121hardhat/8.png)
</li>
</ul>


测试exp
```
forge test --contracts ./src/test/MooCAKECTX_exp.sol -vvvvv
```

(待更新)


#### Tenderly模拟执行

<ul>
<li  markdown="1" style="list-style-type: none;">
![1]({{site.url}}/screenshot/20221121hardhat/7.png)
</li>
</ul>

#### 使用 foundry 编写并学习闪电贷

```js
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.10;

import "forge-std/Test.sol";
import "./interface.sol";


contract testFoundry is DSTest{
    CheatCodes cheats = CheatCodes(0x7109709ECfa91a80626fF3989D68f67F5b1DD12D);
    IERC20 WBNB = IERC20(0xbb4CdB9CBd36B01bD1cBaEBF2De08d9173bc095c);
    IERC20 BUSD = IERC20(0xe9e7CEA3DedcA5984780Bafc599bD69ADd087D56);
    address constant dodo = 0x0fe261aeE0d1C4DFdDee4102E82Dd425999065F4;

    IERC20 WETH = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
    IAaveFlashloan AaveFlash = IAaveFlashloan(0x7d2768dE32b0b80b7a3454c06BdAc94A69DDc7A9);// lendingpool V2

    IDaiFlashloan DaiFlash = IDaiFlashloan(0x1EB4CF3A948E7D72A198fe073cCb8C7a948cD853);

    function setUp() public{
        cheats.label(address(dodo), "DoDo");
        //cheats.createSelectFork("bsc", 20245522);
        cheats.label(address(AaveFlash), "AaveFlash");
        //cheats.createSelectFork("mainnet", 14636844);
        cheats.createSelectFork("mainnet", 16117526);
        console.log("fork eth...");
    }

    function testExploit() public{
        console.log("start");
        // //fork bsc
        // //DODO一个种类的池只支持 _BASE_TOKEN_,_QUOTE_TOKEN_由owner init设置，不收取手续费，并且同时借2个币，可以只偿还其中一种
        // DVM(dodo).flashLoan(5 * 1e18, 400_000 * 1e18, address(this), new bytes(1));
        
        //fork eth 
        //AAVE闪电贷，种类，数量，Mode是模式0 手续费是0.09%,如果Modes的模式选择 1,2(不立即偿还会有债务，1稳定模式，2可变模式)
        //address(WBNB).call{value: 2 ether}(""); foundry请求资产 
        address(WETH).call{value: 1 ether}("");
        emit log_named_decimal_uint("balance before exploit WETH",WETH.balanceOf(address(this)),18);
        address[] memory assets = new address[](1);
        assets[0] = address(WETH);
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 50 * 1e18;
        uint256[] memory modes = new uint[](1);
        modes[0] = 0;
        console.log("Aave FlashLoan 1 WETH");
        AaveFlash.flashLoan(address(this), assets, amounts, modes, address(this), "", 0);
        emit log_named_decimal_uint("balance final exploit WETH",WETH.balanceOf(address(this)),18);

        console.log("end");
    }
    //DoDo的回调
    function DPPFlashLoanCall(address sender, uint256 baseAmount, uint256 quoteAmount, bytes calldata data) external{
        emit log_named_decimal_uint("balance after exploit BUSD",BUSD.balanceOf(address(this)),18);
        emit log_named_decimal_uint("balance after exploit WBNB",WBNB.balanceOf(address(this)),18);
        //call函数例子
        //address(0x96bF2E6CC029363B57Ffa5984b943f825D333614).call(abi.encode(bytes4(0x8a809095)));
        BUSD.transfer(dodo, 400_000 * 1e18);
        WBNB.transfer(dodo, 5 * 1e18);
        emit log_named_decimal_uint("balance after exploit BUSD",BUSD.balanceOf(address(this)),18);
        emit log_named_decimal_uint("balance after exploit WBNB",WBNB.balanceOf(address(this)),18);
    }
    //AAVE的回调
    function executeOperation(
        address[] calldata assets,
        uint256[] calldata amounts,
        uint256[] calldata premiums,
        address initiator,
        bytes calldata params
    ) external payable returns (bool) {
        //emit log_named_decimal_uint("balance after exploit",WBNB.balanceOf(address(this)),18);
        emit log_named_decimal_uint("balance after exploit WETH",WETH.balanceOf(address(this)),18);
        WETH.approve(address(AaveFlash), type(uint).max);
        //WETH.transfer(address(AaveFlash), 1.0009 * 1e18); //授权自动收回,归还手续费
        emit log_named_decimal_uint("balance after exploit WETH",WETH.balanceOf(address(this)),18);
        console.log("Repay Aave FlashLoan");
        return true;
    }

    receive() payable external{}
}
```

#### foundry一些补充

forge - 建立编译测试本地智能合约
cast - 使用已部署的智能合约执行链上交易
Anvil - 一个用于开发目的的本地 TestNet 节点，可以分叉预先存在的网络
(直接运行命令行 anvil开启本地节点 anvil --fork-url https://rpc.ankr.com/bsc)

部署合约例子：
```
forge create --rpc-url https://mainnet.infura.io --private-key abc123456789 src/MyContract.sol:MyContract --constructor-args "Hello Foundry" "Arg2"
```


本地合约的测试方法一？
直接继承foundry Test库，在setup中fork

remix的测试方法？
anvil开启后使用foundry插件

如何导入openzeppelin?
```
forge install OpenZeppelin/openzeppelin-contracts --no-commit
```
可能会安装失败。
本地配置，下载好openzep包之后重新配置remappings例如

```
[profile.default]
src = 'src'
out = 'out'
libs = ['lib']
remappings = ['forge-std/=lib/forge-std/src/','@openzeppelin/=lib/openzeppelin-contracts/']
```

接下来在remix中选择对应的选项即可

<ul>
<li  markdown="1" style="list-style-type: none;">
![1]({{site.url}}/screenshot/20221121hardhat/9.png)
</li>
</ul>


常用的VM中的函数，此文件通常位于
./src/test/Test.sol中可以查看具体的VM函数
```
Vm.prank   //更改调用者
Vm.stopPrank   //更改停止当前的调用者恢复默认
Vm.deal   //更改余额
Vm.selectFork  //fork rpc
```

anvil的常用工具创建私链进行测试
```
Anvil -f --fork-url RPC //FORK
anvil --accounts 15 --balance 300  //设置类似remix账户数量和余额
```

cast计算签名和打包
```
cast sig "transfer(address,uint256)"   //算函数签名
cast keccak data //算hash
```


#### 小坑

1、在编写地址类型的接口实现是，使用浏览器上原本合约的地址复制，不要使用规则过大小写的地址，这会导致foundry无法识别或识别错误。


2、同步Git项目时如果有给定版本的库链接文件，直接拉取下载包可能有一些玄学问题，推荐使用git clone

3、在remapping.txt中例如
```js
//配置文件可以指定库的位置，在solidity代码中有@openzeppelin(重名)会优先使用本地链接库,如果需要自动拉取则需要先删除/添加此路径
ds-test/=lib/solmate/lib/ds-test/src/
forge-std/=lib/forge-std/src/
openzeppelin-contracts/=lib/openzeppelin-contracts/
solmate/=lib/solmate/src/
```
